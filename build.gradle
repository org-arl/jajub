plugins {
  id 'java-library'
  id 'groovy'
  id 'maven'
  id 'signing'
}

group = 'com.github.org-arl'
version = '0.1.0'

defaultTasks 'jar'

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
  maven { url 'https://oss.sonatype.org/content/groups/public' }
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
  testImplementation 'org.codehaus.groovy:groovy-all:2.5.8'
  testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
  testImplementation 'junit:junit:4.12'
}

jar {
  manifest {
    attributes 'Build-Owner': System.getenv().USER, 'Build-Timestamp': new Date().format('d-MM-yyyy_HH:mm:ss'), 'Build-Version': version
  }
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')
javadoc.destinationDir = new File('docs')

////// tasks for MavenCentral deployment

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment {
        MavenDeployment deployment -> signing.signPom(deployment)
      }
      if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
      }
      pom.project {
        name archivesBaseName
        packaging 'jar'
        description 'Java-Julia bridge'
        url 'http://github.com/org-arl/jajub'
        scm {
          url 'http://github.com/org-arl/jajub'
          connection 'scm:git:git://github.com/org-arl/jajub.git'
          developerConnection 'scm:git:git@github.com:org-arl/jajub.git'
        }
        licenses {
          license {
            name 'MIT License'
            url 'https://github.com/org-arl/jajub/blob/master/LICENSE.txt'
            distribution 'repo'
          }
        }
        developers {
          developer {
            id 'mchitre'
            name 'Mandar Chitre'
          }
        }
      }
    }
  }
}
